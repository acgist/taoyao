plugins {
    id 'com.android.library'
}

def WEBRTC_INC_PATH = "${projectDir}/deps/webrtc/src"
def WEBRTC_LIB_PATH = "${projectDir}/deps/webrtc/lib"

android {
    namespace 'com.acgist.taoyao.media'
    compileSdk rootProject.ext.taoyao.compileSdk
    ndkVersion rootProject.ext.taoyao.ndkVersion
    defaultConfig {
        minSdk rootProject.ext.taoyao.minSdk
        targetSdk rootProject.ext.taoyao.targetSdk
        versionCode rootProject.ext.taoyao.versionCode
        versionName rootProject.ext.taoyao.versionName
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                cFlags '-std=c17'
                cppFlags '-std=c++17'
                // CPU架构：x86、x86_64、arm64-v8a、armeabi-v7a
                abiFilters 'arm64-v8a'
                arguments "-DLIBWEBRTC_INCLUDE_PATH=" + WEBRTC_INC_PATH,
                    "-DLIBWEBRTC_BINARY_PATH=" + WEBRTC_LIB_PATH,
                    "-DMEDIASOUPCLIENT_BUILD_TESTS=OFF",
                    "-DMEDIASOUPCLIENT_LOG_TRACE=OFF",
                    "-DMEDIASOUPCLIENT_LOG_DEV=OFF",
                    "-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility rootProject.ext.taoyao.javaVersion
        targetCompatibility rootProject.ext.taoyao.javaVersion
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ["libs"]
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
            version rootProject.ext.taoyao.cmakeVersion
        }
    }
}

dependencies {
    api project(path: ':boot')
    api 'androidx.annotation:annotation:1.3.0'
    testImplementation 'junit:junit:4.13.2'
}
